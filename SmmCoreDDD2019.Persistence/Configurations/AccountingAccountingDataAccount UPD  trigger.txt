CREATE TRIGGER Accounting.trg_AccountingDataAccount_UPD
	ON Accounting.AccountingDataAccount
	INSTEAD OF UPDATE
AS 
BEGIN
	SET NOCOUNT ON;

	--Don't allow updates to structural fields (Position, Depth) nor parent; really, you can only update Name.
	IF (EXISTS (SELECT 1
		FROM Deleted
		JOIN Inserted
			ON Inserted.NoUrutAccountId = Deleted.NoUrutAccountId
		WHERE Deleted.Parent <> Inserted.Parent
			OR Deleted.Lft <> Inserted.Lft
			OR Deleted.Rgt <> Inserted.Rgt
			OR Deleted.Depth <> Inserted.Depth
		))
	BEGIN
		RAISERROR (N'Cannot update position/parent values inline; use the dedicated stored-procedure to move nodes.', 18, 1)
		RETURN
	END

	--Don't allow updates to NULL values
	IF (EXISTS (SELECT 1
		FROM Deleted
		JOIN Inserted
			ON Inserted.NoUrutAccountId = Deleted.NoUrutAccountId
		WHERE Inserted.Parent IS NULL
			OR Inserted.Lft IS NULL
			OR Inserted.Rgt IS NULL
			OR Inserted.Depth IS NULL
		))
	BEGIN
		RAISERROR (N'Cannot update values to NULL; use the dedicated stored-procedure to set a node to "root" (NULL parent).', 18, 1)
		RETURN
	END

	--Else, proceed with update -- you can only update the Name!
	UPDATE Accounting.AccountingDataAccount SET Accounting.AccountingDataAccount.Account = Inserted.Account
	FROM Accounting.AccountingDataAccount
	JOIN Inserted
		ON Inserted.NoUrutAccountId = Accounting.AccountingDataAccount.NoUrutAccountId
END
GO
